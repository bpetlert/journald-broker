# Maintainer: Bhanupong Petchlert <bpetlert@gmail.com>
pkgname=journald-broker
pkgver=0.5.0
pkgrel=1
pkgdesc="A log-based event dispatcher daemon for systemd's journal"
arch=('x86_64')
url="https://github.com/bpetlert/journald-broker"
license=('GPL-3.0-or-later')
depends=('systemd-libs')
makedepends=(cargo)
options=(!lto)
provides=("${pkgname}")
conflicts=("${pkgname}")
backup=(etc/systemd/journald-broker.toml)

# Build from local directory
source=()

# Using the most recent annotated tag reachable from the last commit.
pkgver() {
  cd "${startdir}"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${startdir}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "${startdir}"

  export RUSTUP_TOOLCHAIN=stable

  # Ignore target-dir in ~/.cargo/config, reset it to default: "target".
  # Add "$srcdir" to support BUILDDIR variable in makepkg.
  export CARGO_TARGET_DIR=$srcdir/target

  cargo build  --frozen --release
}

package() {
  cd "$srcdir"
  install -Dm755 "target/release/journald-broker" "$pkgdir/usr/bin/journald-broker"

  install -Dm644 "${startdir}/journald-broker.service" "$pkgdir/usr/lib/systemd/system/journald-broker.service"

  install -Dm644 "${startdir}/journald-broker.toml" "$pkgdir/etc/systemd/journald-broker.toml"

  install -Dm644 "${startdir}/README.adoc" "$pkgdir/usr/share/doc/${pkgname}/README.adoc"
  install -Dm644 "${startdir}/COPYING" "$pkgdir/usr/share/licenses/${pkgname}/COPYING"
}
