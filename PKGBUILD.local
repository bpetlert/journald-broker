# Maintainer: Bhanupong Petchlert <bpetlert@gmail.com>
pkgname=journald-broker
pkgver=0.7.0
pkgrel=1
pkgdesc="A log-based event dispatcher daemon for systemd's journal"
arch=('x86_64')
url="https://github.com/bpetlert/journald-broker"
license=('GPL-3.0-or-later')
depends=('systemd-libs')
makedepends=(cargo)
options=(!lto)
backup=(
  "etc/journald-broker.d/00-global-settings.conf"
  "etc/journald-broker.d/00-template.conf"
)

# Build from local directory
source=()

# Using the most recent annotated tag reachable from the last commit.
pkgver() {
  cd "${startdir}"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${startdir}"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${startdir}"
  export RUSTUP_TOOLCHAIN=stable

  # Ignore target-dir in ~/.cargo/config, reset it to default: "target".
  # Add "${srcdir}" to support BUILDDIR variable in makepkg.
  export CARGO_TARGET_DIR=${srcdir}/target

  cargo build --frozen --release
}

package() {
  install -Dm755 "${srcdir}/target/release/journald-broker" "${pkgdir}/usr/bin/journald-broker"

  install -Dm644 "${startdir}/journald-broker.service" "${pkgdir}/usr/lib/systemd/system/journald-broker.service"

  install -dm755 "${pkgdir}/etc/journald-broker.d/"
  install -Dm644 "${startdir}/00-global-settings.conf" "${pkgdir}/etc/journald-broker.d/00-global-settings.conf"
  install -Dm644 "${startdir}/00-template.conf" "${pkgdir}/etc/journald-broker.d/00-template.conf"

  install -Dm644 "${startdir}/README.adoc" "${pkgdir}/usr/share/doc/${pkgname}/README.adoc"
}
